buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
		classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'

uploadArchives {
    repositories.mavenInstaller {
        pom.groupId = 'com.github.mirko02'
        pom.artifactId = 'linearlistview'
        pom.version = '0.0.1'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "22.0.1"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}
